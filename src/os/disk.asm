; TITLE 'DISK QUADRANT'
;
; APR 14, 2022
;
	.PROJECT disk.com

STACK   EQU     000F0H
CMDSND	EQU	005DDH
RECSEND	EQU	00AEDH
RECRECV	EQU	00CEDH
MOVXB   EQU     070DDH

	.ORG    0FF00H

START:  LXI     SP, STACK
WAIT:	MVI     C, 0    ;CMD NOP
SEND:   DW      CMDSND  ;SEND CMD; YIELD
	MOV	A, C    ;A=CMD
	CPI	04H	;LIMIT CMD<4
	JNC     WAIT    ;SKIP HIGH CMD
	MOV     A, D    ;A=00TTTTTT
	CPI     63
	JNC     WAIT    ;SKIP TRACKS>62
	MOV	A, E    ;A=000QQSSS - 4 QUADS OF 8 SECTORS
	RRC             ;A=S000QQSS
	RRC             ;A=SS000QQS
	RRC             ;A=SSS000QQ
	ANI	0E0H    ;A=SSS00000 - CLEAR QUAD
	MOV	E, A
	XCHG            ;HL=00TTTTTTSSS00000
	DAD	H       ;HL=0TTTTTTSSS000000
	DAD	H       ;HL=TTTTTTSSS0000000
	INR     H       ;HL+0000000100000000
	PUSH	H       ;SAVE HL
	LXI	H, TABLE
	MVI	D, 0
	MOV	A, C	;A=CMD
	ADD	A	;A*=2
	MOV	E, A	;OFFSET
	DAD	D	;ADD TO TABLE
	MOV	E, M	;LOW BYTE
	INX	H
	MOV	D, M	;HIGH BYTE
	XCHG		;INTO HL
	POP     D       ;DE==TTTTTTSSS0000000
	PCHL		;GO THERE
;
; DISK GETS RECORD
; - COPY FROM MEM [DE] TO BUFFER
; - SEND RETURN
;
GET:	XRA     A       ;START SHM@0
        DW	RECSEND	;COPY FROM MEM
	MVI     C, 1    ;CMD RETURN
	JMP	SEND    ;SEND RETURN
;
; DISK PUTS RECORD
; - COPY FROM BUFFER TO MEM [DE]
; - SEND RETURN
;
PUT:    XRA     A       ;START SHM@0
        DW	RECRECV	;COPY TO MEM
	MVI     C, 1    ;CMD RETURN
	JMP	SEND    ;SEND RETURN
;
; COMMAND JUMP VECTOR TABLE
;
TABLE:  DW      START   ;RESET
	DW      WAIT    ;RETURN N/A
	DW      GET     ;CMD 2: GET
	DW      PUT     ;CMD 3: PUT
