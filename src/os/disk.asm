; TITLE 'DISK QUADRANT'
;
; JAN 25, 2023
;
	.PROJECT disk.com

STACK   EQU     000FFH
CMDSND	EQU	005DDH
RECSEND	EQU	00AEDH
RECRECV	EQU	00CEDH
MOVXB   EQU     070DDH

	.ORG    0FF00H

START:  LXI     SP, STACK
WAIT:	MVI     C, 0    ;CMD NOP
SEND:   DW      CMDSND  ;SEND CMD; YIELD
	MOV	A, C    ;A=CMD
	CPI	4       ;CMD>=4?
	JNC     WAIT    ;SKIP HIGH CMD
	MOV     A, D
	CPI     254     ;TRACK>=254?
	JNC     WAIT    ;SKIP HIGH TRACK
	INR     D       ;AVOID ZERO PAGE
	MOV	A, E    ;A=00000SQQ - 4 QUADS OF 2 SECTORS
	RRC             ;A=Q00000SQ
	RRC             ;A=QQ00000S
	RRC             ;A=SQQ00000
	ANI	080H    ;A=S0000000 - CLEAR QUAD
	MOV     E, A
	PUSH	D       ;SAVE DE
	LXI	H, TABLE
	MVI	D, 0
	MOV	A, C	;A=CMD
	ADD	A	;A*=2
	MOV	E, A	;OFFSET
	DAD	D	;ADD TO TABLE
	MOV	E, M	;LOW BYTE
	INX	H
	MOV	D, M	;HIGH BYTE
	XCHG		;INTO HL
	POP     D       ;DE==TTTTTTTTS0000000
	PCHL		;GO THERE
;
; DISK GETS RECORD
; - COPY FROM MEM [DE] TO BUFFER
; - SEND RETURN
;
GET:	XRA     A       ;START SHM@0
        DW	RECSEND	;COPY FROM MEM
	MVI     C, 1    ;CMD RETURN
	JMP	SEND    ;SEND RETURN
;
; DISK PUTS RECORD
; - COPY FROM BUFFER TO MEM [DE]
; - SEND RETURN
;
PUT:    XRA     A       ;START SHM@0
        DW	RECRECV	;COPY TO MEM
	MVI     C, 1    ;CMD RETURN
	JMP	SEND    ;SEND RETURN
;
; COMMAND JUMP VECTOR TABLE
;
TABLE:  DW      START   ;RESET
	DW      WAIT    ;RETURN N/A
	DW      GET     ;CMD 2: GET
	DW      PUT     ;CMD 3: PUT
