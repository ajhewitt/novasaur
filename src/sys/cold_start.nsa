# Cold Start - memory test, blank bank0, set mem size
INCLUDE ../inc/unary.nsa
INCLUDE ../inc/zpage.nsa
INCLUDE ../inc/pages.nsa
PAGE COLD_START_PG

LD HL, $0x11              # LED OFF for preflight
MVHL A, EA                # bank1,LED off->E
LD HL, $0
MULL A, V                 # V=0
MULL A, S                 # S=0
MULL A, X                 # X=0
LD Y, $0                  # Y=0
$PREFLIGHT
LD HL, $0xAA
MVHL A, NM                # AA->[0x10000]
XORHL A, NA               # A=0
XORHL M, NA               # A=[0x10000]$AA
LD HL, $ZERO?$NULL
FNH A, HLA                # A==0?
LDN PC, $PREFLIGHT        # try again?
LD HL, $0x55
MVHL A, NM                # 55->[0x10000]
XORHL A, NA               # A=0
XORHL M, NA               # A=[0x10000]$55
LD HL, $ZERO?$NULL
FNH A, HLA                # A==0?
LDN PC, $PREFLIGHT        # try again?

LD HL, $0                 # LED ON for fill
MULL A, E                 # bank0,LED on->E
LD Y, $0x80
MULH AZ, ND               # 0->zpageY
$ROW1
LDZ HL, $IDEN$NULL
FNH DZ, Y                 # Y=zpageY
LD HL, $0xFF
MVHL AZ, XD               # X=FF, FF->zpageX
$FILL1
LD HL, $INC$NULL
FNFH DZ, XD               # X+1->zpageX
FNH A, HLD                # A->[YYXX]
LD HL, $ZERO?$NULL
FNH A, HLA                # X==0?
LDN PC, $FILL1            # loop if X<=255
LD HL, $INC$NULL
LDZ Y, $0x80
FNH DZ, HLD               # Y+1->zpageY
LDP PC, $ROW1             # loop if Y<80

LD HL, $0x10              # LED OFF for check
MVHL A, EA                # bank0,LED off->E
LD Y, $0x80
LD HL, $0
MULH AZ, ND               # 0->zpageY
$ROW2
LDZ HL, $IDEN$NULL
FNH DZ, Y                 # Y=zpageY
LD HL, $0
MULL A, X                 # X=0, 0->zpageX
$CHECK2
LDZ HL, $INC$NULL
FNH DZ, HL                # HL=zpageX+1
XORHL D, ND               # X$[YYXX]->[YYXX]
LD HL, $ZERO?$NULL
FNH D, HLA                # [YYXX]==0?
LDN PC, $0                # retest if !0
LD HL, $INC$NULL
FNFH DZ, XD               # X+1->zpageX
FNH A, HLA                # A=0 if X==FF
LD HL, $ZERO?$NULL
FNH A, HLA                # X==FF?
LDN PC, $CHECK2           # loop if X>0
LD HL, $0
MULH A, ND                # 0->[YYFF]
LD HL, $INC$NULL
LDZ Y, $0x80
FNH DZ, HLD               # Y+1->zpageY
LDP PC, $ROW2             # loop if Y<80

LD HL, $0                 # LED ON for fill
MULL A, E                 # bank0,LED on->E
LD HL, $0x80
LD Y, $0x7F
MVHL AZ, ND               # 80->zpageY
$ROW3
LDZ HL, $IDEN$NULL
FNH DZ, Y                 # Y=zpageY
LD HL, $0xFF
MVHL AZ, XD               # X=FF, FF->zpageX
$FILL3
LD HL, $INC$NULL
FNFH DZ, XD               # X+1->zpageX
FNH A, HLD                # A->[YYXX]
LD HL, $ZERO?$NULL
FNH A, HLA                # X==0?
LDN PC, $FILL3            # loop if X<=255
LD HL, $INC$NULL
LDZ Y, $0x7F
FNH DZ, HLD               # Y+1->zpageY
LDN PC, $ROW3             # loop if Y>80

LD HL, $0x10              # LED OFF for check
MVHL A, EA                # bank0,LED off->E
LD HL, $0x80
LD Y, $0x7F
MVHL AZ, ND               # 0x80->zpageY
$ROW4
LDZ HL, $IDEN$NULL
FNH DZ, Y                 # Y=zpageY
LD HL, $0
MULL A, X                 # X=0, 0->zpageX
$CHECK4
LDZ HL, $INC$NULL
FNH DZ, HL                # HL=zpageX+1
XORHL D, ND               # X$[YYXX]->[YYXX]
LD HL, $ZERO?$NULL
FNH D, HLA                # [YYXX]==0?
LDN PC, $0                # retest if !0
LD HL, $INC$NULL
FNFH DZ, XD               # X+1->zpageX
FNH A, HLA                # A=0 if X==FF
LD HL, $ZERO?$NULL
FNH A, HLA                # X==FF?
LDN PC, $CHECK4           # loop if X>0
LD HL, $0
MULH A, ND                # 0->[YYFF]
LD HL, $INC$NULL
LDZ Y, $0x7F
FNH DZ, HLD               # Y+1->zpageY
LDN PC, $ROW4             # loop if Y>80

LD HL, $0x01
LD Y, $EO
MVHL AZ, ED               # bank1,LED on->E *** init EO

LD HL, $DEC$NULL
LDZ Y, $TIME3
FNH DZ, HLD               # set days to -1 (will overflow to 0 on start)
LD HL, $0xA0              # green on black, font 2
LD Y, $CONF
MVHL AZ, ND               # set default text colors
LD HL, $0x5F              # '_'
LD Y, $CONC
MVHL AZ, ND               # set default console cursor
LD Y, $MODE               # VGA75 - 80x30 text
LD HL, $3
MVHL AZ, ND               # set default video mode

LD PG, $KBD_START_PG
