# Cold Start - memory test, blank bank0, set mem size
INCLUDE ../inc/unary.nsa
INCLUDE ../inc/zpage.nsa
INCLUDE ../inc/pages.nsa
PAGE COLD_START

LD HL, $0x01              # LED ON for preflight
MVHL A, EA                # bank1,LED on->E
MULH A, VA                # V=0
MULH A, XA                # X=0
LD Y, $0                  # Y=0
$PREFLIGHT
LD HL, $0xAA
MVHL A, NM                # AA->[0x10000]
XORHL A, NA               # A=0
XORHL M, NA               # A=[0x10000]$AA
LD HL, $ZERO?$NULL
FNH A, HLA                # A==0?
LDN PC, $PREFLIGHT        # try again?
LD HL, $0x55
MVHL A, NM                # 55->[0x10000]
XORHL A, NA               # A=0
XORHL M, NA               # A=[0x10000]$55
LD HL, $ZERO?$NULL
FNH A, HLA                # A==0?
LDN PC, $PREFLIGHT        # try again?

LD HL, $0x10              # LED OFF for fill
MVHL A, EA                # bank0,LED off->E
LD HL, $0
LD Y, $0x80
MULH AZ, ND               # 0->zpageY
$ROW1
LDZ HL, $IDEN$NULL
FNH DZ, Y                 # Y=zpageY
LD HL, $0xFF
MVHL AZ, XD               # X=FF, FF->zpageX
$FILL1
LD HL, $INC$NULL
FNFH DZ, XD               # X+1->zpageX
FNH A, HLD                # A->[YYXX]
LD HL, $OVER?$NULL
FNH A, HLA                # X==0?
LDN PC, $FILL1            # loop if X<=255
LD HL, $INC$NULL
LDZ Y, $0x80
FNH DZ, HLD               # Y+1->zpageY
LDP PC, $ROW1             # loop if Y<80

LD HL, $0x40
MVHL A, SA
MVHL A, SA                # kbdtx high,venable on

LD HL, $0x00              # LED ON for check
MVHL A, EA                # bank0,LED on->E
LD Y, $0x80
MULH AZ, ND               # 0->zpageY
$ROW2
LDZ HL, $IDEN$NULL
FNH DZ, Y                 # Y=zpageY
LD HL, $0
MULL A, X                 # X=0, 0->zpageX
$CHECK2
LDZ HL, $INC$NULL
FNH DZ, HL                # HL=zpageX+1
XORHL D, ND               # X$[YYXX]->[YYXX]
LD HL, $ZERO?$NULL
FNH D, HLA                # [YYXX]==0?
LDN PC, $0                # retest if !0
LD HL, $INC$NULL
FNFH DZ, XD               # X+1->zpageX
LD HL, $UNDER?$NULL
FNH A, HLA                # X==FF?
LDP PC, $CHECK2           # loop if X>0
LD HL, $0
MULH A, ND                # 0->[YYFF]
LD HL, $INC$NULL
LDZ Y, $0x80
FNH DZ, HLD               # Y+1->zpageY
LDP PC, $ROW2             # loop if Y<80

LD HL, $0x00
MVHL A, SA
MVHL A, SA                # kbdtx high,venable off

LD HL, $0x10              # LED OFF for fill
MVHL A, EA                # bank0,LED off->E
LD HL, $0x80
LD Y, $0x7F
MVHL AZ, ND               # 80->zpageY
$ROW3
LDZ HL, $IDEN$NULL
FNH DZ, Y                 # Y=zpageY
LD HL, $0xFF
MVHL AZ, XD               # X=FF, FF->zpageX
$FILL3
LD HL, $INC$NULL
FNFH DZ, XD               # X+1->zpageX
FNH A, HLD                # A->[YYXX]
LD HL, $OVER?$NULL
FNH A, HLA                # X==0?
LDN PC, $FILL3            # loop if X<=255
LD HL, $INC$NULL
LDZ Y, $0x7F
FNH DZ, HLD               # Y+1->zpageY
LDN PC, $ROW3             # loop if Y>80

LD HL, $0x00              # LED OFF for check
MVHL A, EA                # bank0,LED on->E
LD HL, $0x80
LD Y, $0x7F
MVHL AZ, ND               # 0x80->zpageY
$ROW4
LDZ HL, $IDEN$NULL
FNH DZ, Y                 # Y=zpageY
LD HL, $0
MULL A, X                 # X=0, 0->zpageX
$CHECK4
LDZ HL, $INC$NULL
FNH DZ, HL                # HL=zpageX+1
XORHL D, ND               # X$[YYXX]->[YYXX]
LD HL, $ZERO?$NULL
FNH D, HLA                # [YYXX]==0?
LDN PC, $0                # retest if !0
LD HL, $INC$NULL
FNFH DZ, XD               # X+1->zpageX
LD HL, $UNDER?$NULL
FNH A, HLA                # X==FF?
LDP PC, $CHECK4           # loop if X>0
LD HL, $0
MULH A, ND                # 0->[YYFF]
LD HL, $INC$NULL
LDZ Y, $0x7F
FNH DZ, HLD               # Y+1->zpageY
LDN PC, $ROW4             # loop if Y>80

LD HL, $0x11              # LED OFF to set mem size
MVHL A, EA                # bank1,LED off->E
LD HL, $0xAA
LD Y, $0xFF               # Y=FF
MVHL AZ, NM               # AA->[1FFFF]
LD HL, $0x13
MVHL A, EA                # bank3->E
LD HL, $0x55
MVHL AZ, NM               # 55->[7FFFF]
LD HL, $0x11
MVHL A, EA                # bank1->E
LD HL, $0xAA
XORHL MZ, NA              # A=AA$[1FFFF]
LD HL, $ZERO?$NULL
FNH A, HLA                # A=0 if 512k, else -1 if 128k
LD HL, $0
LD Y, $HWSTAT
ANDH AZ, ND               # mem size->HW STAT H

LD PG, $KBD_START
