# Video Start - set video mode and start HAL
INCLUDE ../inc/unary.nsa
INCLUDE ../inc/zpage.nsa
INCLUDE ../inc/pages.nsa
PAGE VID_START_PG

$INDEX 0x80

LD HL, $3                 # -0x7D
LD Y, $RCODE
ADDHL DZ, NA
LD HL, $ZERO?$NULL
FNH A, HLA                # page up?
LDP HL, $INC$NULL
LDN HL, $IDEN$NULL
LDZ Y, $VMODE
FNH DZ, HLD               # VMODE+1

LD HL, $6                 # -0x7A
LD Y, $RCODE
ADDHL DZ, NA
LD HL, $ZERO?$NULL
FNH A, HLA                # page down?
LDP HL, $DEC$NULL
LDN HL, $IDEN$NULL
LDZ Y, $VMODE
FNH DZ, HLD               # VMODE-1

LD HL, $0
LD Y, $RCODE
MULH AZ, ND               # clear restart code

LD HL, $SWAP$NULL
LDZ Y, $VMS
FNH DZ, HLD               # swap VMS

LD HL, $0x40
LD Y, $VMODE
MULH DZ, NA               # A = VMODE<<2
LD HL, $IDEN$NULL
LD Y, $INDEX
FNH AZ, HLD               # mode index->temp

LD HL, $VATTR$NULL
FNCH A, NA                # A = VMODES
LD HL, $IDEN$NULL
LDZ Y, $MODE
FNH AZ, HLD               # set video mode

LD HL, $SWAP$NULL
FNH A, HLA                # H = VMODEL
LD Y, $VMS
MVH DZ, NA                # H -> VMSL
LD HL, $SWAP$NULL
FNH AZ, HLD               # swap VMS

LD HL, $INC$NULL
LDZ Y, $INDEX
FNH DZ, HLD               # mode index+1->temp

LD HL, $VATTR$NULL
FNCH A, NA                # A = ELEFT
LD HL, $0x3F
ANDHL A, NA               # A = LEFT BORDER
LD HL, $IDEN$NULL
LD Y, $CONL
FNFH AZ, ND               # left border -> console left
LD Y, $CONX
FNFH AZ, ND               # left border -> console X
LD Y, $CONB
FNH AZ, HLD               # left border -> console backstop

LD HL, $0x3F
LD Y, $EO
ANDHL DZ, ND              # Eo&3F -> Eo
LD HL, $VATTR$NULL
LD Y, $INDEX
FNCH DZ, NA               # A = ELEFT
LD HL, $0xC0
ANDHL A, NA               # A = Eo delta
LD HL, $IDEN$NULL
FNH A, HL                 # HL = Eo delta
LD Y, $EO
ORHL DZ, ED               # set text/VDAC bits

LD HL, $INC$NULL
LDZ Y, $INDEX
FNH DZ, HLD               # mode index+1->temp

LD HL, $VATTR$IDEN
FNCH A, NA                # A = WIDTH
LD Y, $CONL
FNFL DZ, HL               # HL = CON left
LD Y, $CONR
ADDHL AZ, ND              # CON left + WIDTH -> Con right

LD HL, $INC$NULL
LDZ Y, $INDEX
FNH DZ, HLD               # mode index+1->temp

LD HL, $VATTR$NULL
FNCH A, NA                # A = HEIGHT
LD HL, $1COM$NULL
LD Y, $CONH
FNH AZ, HLD               # -height-1 -> CON height

LD HL, $0
LD Y, $VSTART
MULH AZ, ND               # 0 -> vstart
LD Y, $CONY               
MULH AZ, ND               # 0 -> console row

LD Y, $VMS
LD PG, $BSYNCE_PG         # start video loop
