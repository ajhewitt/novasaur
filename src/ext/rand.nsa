# 16-bit PRNG seeded from BC using BC^=BC<<1;BC^=BC>>1;BC^=BC<<15
# ref: https://en.wikipedia.org/wiki/Xorshift
INCLUDE ../inc/unary.nsa
INCLUDE ../inc/zpage.nsa
INCLUDE ../inc/pages.nsa
PAGE RAND_PG

# assume: Y = $VMS
LDZ HL, $INC2$NULL
FNH DZ, HLD               # inc state
LD HL, $IDEN$LSL
LD Y, $CREG
FNFH DZ, NA               # A=Creg
#11
LDP PC, $NOCARRY13        # CregMSB==0?
FNFL A, HL                # HL=Creg<<1
XORHL DZ, ND              # Creg^=Creg<<1
#19
LD HL, $LSL$1
LD Y, $BREG
FNFH DZ, NA               # A=Breg<<1
ORL A, HL                 # HL=Breg<<1+CregMSB
#29
$CONT29
XORHL DZ, ND              # Breg^=Breg<<1
LD HL, $REVERSE$NULL
FNH A, HLA
LDP HL, $LSR$NULL         # BregLSB==0
LDN HL, $NSR$NULL         # BregLSB==1
#40
LDZ Y, $CREG
FNH DZ, HL                # HL=BregLSB+Creg>>1
XORHL DZ, ND              # Creg^=Creg>>1
LD HL, $LSR$NULL
#50
LDZ Y, $BREG
FNH DZ, HL                # HL=Breg>>1
XORHL DZ, ND              # Breg^=Breg>>1
LD HL, $REVERSE$NULL
#60
LDZ Y, $CREG
FNH DZ, HLA
LDP HL, $0                # CregLSB==0
LDN HL, $0x80             # CregLSB==1
LD Y, $BREG
#69
XORHL DZ, ND              # Breg^=BCreg<<15
NOP
NOP
NOP
NOP
NOP
LD HL, $FETCH
LD Y, $VMS                # set Y = $VMS on exit
DISHL DZ, PGA             # jump to next VMC
#86

$NOCARRY13
FNFL A, HL                # HL=Creg<<1
XORHL DZ, ND
#20
LD HL, $LSL$NULL
LDZ Y, $BREG
FNH DZ, HL                # HL=Breg<<1
NOP
LD PC, $CONT29
